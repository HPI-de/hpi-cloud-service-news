plugins {
    id 'application'
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.3.10'
    id 'com.github.johnrengelman.shadow' version '4.0.4'

    id 'com.google.protobuf' version '0.8.5'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

mainClassName = "de.hpi.cloud.services.news.MainKt"
shadowJar {
    archiveName = "service-news.jar"
}


repositories {
    mavenCentral()
    mavenLocal()
}

def grpcVersion = '1.19.0'

dependencies {
    def kotlinVersion = plugins.findPlugin("org.jetbrains.kotlin.jvm").kotlinPluginVersion
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"

    // gRPC
    compile "io.grpc:grpc-netty-shaded:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-services:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    protobuf files("src/main/hpi-cloud-apis")

    // Testing
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:1.9.5"
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.6.1' }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}
compileKotlin {
    dependsOn(':generateProto')
}

jar {
    manifest {
        attributes 'Main-Class': 'de.hpi.cloud.services.news.MainKt'
    }
}
